/**
 Copyright 2012 Bertrand Florat
 Executable specifications build script 
 **/
import org.apache.tools.ant.filters.ReplaceTokens

defaultTasks('replaceInternalDefinition');

dependencies() {
	//tests
	testCompile('org.concordion:concordion:1.4.2');
	testCompile(project(':org.m2ling.common'));
	testCompile(project(':org.m2ling.service'));
	testCompile(files("../org.m2ling.common/build/classes/test"));
}

File concordionOutputDir = file("${buildDir}/concordion-results");


sourceSets{
	test{
		// We need to override the default src/test/resources maven directory here because
		// concordion seems to forces html and fixtures classes being in the same directory.
		java(){ srcDir 'src/specs/java' }
		resources(){ srcDir 'src/specs/java' }
	}
}

test {
	systemProperties('concordion.output.dir': concordionOutputDir);
	// Exclude classes from org.m2ling.specs : concordion integration code, not actual tests
	exclude 'org/m2ling/specs/*.*'
}


task buildSpecs(dependsOn:['clean', 'test'])<<{
	copy{
		description 'Copy static HTML along with fixtures generated HTML files...'
		from 'src/specs/java'
		into concordionOutputDir
		include '**/*_static.html'
		include '**/index.html','*.css'
	}
}

Map internalDefinitionsAcronymMap = [:]


task loadGlossary<<{
	def glossary = new XmlParser().parse('glossary/m2ling.m2ling_glossary')
	glossary.contents.each {
		String name = it.attribute("name")
		String internalDefinition = it.attribute("internal_definition")
		internalDefinitionsAcronymMap.put(name,"<acronym title='"+internalDefinition+"'>"+name+"</acronym>" )
	}
}

/*
 * Replace all glossary entries.
 * 
 * Note that the substitution is case sensitive, by convention all glossary entries must begin by an uppcase :
 * Examples : Parameter, ParameterDefinition
 * 
 * ~entry~ to a href link. 
 * ?entry? to a note
 */
task replaceInternalDefinition(dependsOn:['buildSpecs', 'loadGlossary'])<<{
	copy {
		from concordionOutputDir
		into "${concordionOutputDir}_tmp"
		include '**/*.*'
		filter(ReplaceTokens, beginToken:'~',endToken:'~', tokens:internalDefinitionsAcronymMap)
	}
	// Gradle don't seem to support the in-place substitution, so we have to make
	// all this boilerplate moves :
	delete concordionOutputDir
	copy{
		from "${concordionOutputDir}_tmp"
		into concordionOutputDir
	}
	delete "${concordionOutputDir}_tmp"
}